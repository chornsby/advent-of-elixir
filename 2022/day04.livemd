<!-- livebook:{"file_entries":[{"name":"day04.txt","type":"attachment"}]} -->

# 2022-12-04

```elixir
Mix.install([:kino])
```

## Solution

```elixir
defmodule Advent do
  @moduledoc """
  Day 4: Camp Cleanup

      iex> input = \"""
      ...> 2-4,6-8
      ...> 2-3,4-5
      ...> 5-7,7-9
      ...> 2-8,3-7
      ...> 6-6,4-6
      ...> 2-6,4-8
      ...> \"""
      ...> Advent.count_full_overlaps(input)
      2
      iex> Advent.count_partial_overlaps(input)
      4
  """

  @doc """
  Return the number of assignments that fully overlap another.

      iex> input = Kino.FS.file_path("day04.txt") |> File.read!()
      ...> Advent.count_full_overlaps(input)
      532
  """
  def count_full_overlaps(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&parse_pairs/1)
    |> Enum.filter(&fully_overlaps?/1)
    |> Enum.count()
  end

  @doc """
  Return the number of assignments that partially overlap another.

      iex> input = Kino.FS.file_path("day04.txt") |> File.read!()
      ...> Advent.count_partial_overlaps(input)
      854
  """
  def count_partial_overlaps(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&parse_pairs/1)
    |> Enum.filter(&partially_overlaps?/1)
    |> Enum.count()
  end

  defp parse_pairs(line) do
    line
    |> String.split(",")
    |> Enum.map(&parse_range/1)
  end

  defp parse_range(range) do
    range
    |> String.split("-")
    |> Enum.map(&String.to_integer/1)
  end

  defp fully_overlaps?([[a, b], [c, d]]) do
    (a <= c and b >= d) or (c <= a and d >= b)
  end

  defp partially_overlaps?([[a, b], [c, d]]) do
    a <= d and b >= c
  end
end
```
