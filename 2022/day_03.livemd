<!-- livebook:{"file_entries":[{"name":"day03.txt","type":"attachment"}]} -->

# 2022-12-03

```elixir
Mix.install([:kino])
```

## Solution

```elixir
defmodule Advent do
  @moduledoc """
  Day 3: Rucksack Reorganization

  ## Examples

      iex> input = \"""
      ...> vJrwpWtwJgWrhcsFMMfFFhFp
      ...> jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
      ...> PmmdzqPrVvPwwTWBwg
      ...> wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
      ...> ttgJtRGJQctTZtZT
      ...> CrZsJsPPZsGzwwsLwLmpwMDw
      ...> \"""
      ...> Advent.calculate_misplaced_item_priorities(input)
      157
      iex> Advent.calculate_common_item_trio_priorities(input)
      70
  """

  @doc """
  Return the sum of the misplaced item priorities.

  ## Examples

      iex> input = Kino.FS.file_path("day03.txt") |> File.read!()
      ...> Advent.calculate_misplaced_item_priorities(input)
      8139
  """
  def calculate_misplaced_item_priorities(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&String.to_charlist/1)
    |> Enum.map(&split_compartments/1)
    |> Enum.flat_map(&find_common_items/1)
    |> Enum.map(&to_priority/1)
    |> Enum.sum()
  end

  @doc """
  Return the sum of item priorities common to sets of three elves.

  ## Examples

      iex> input = Kino.FS.file_path("day03.txt") |> File.read!()
      ...> Advent.calculate_common_item_trio_priorities(input)
      2668
  """
  def calculate_common_item_trio_priorities(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&String.to_charlist/1)
    |> Enum.chunk_every(3)
    |> Enum.flat_map(&find_common_items/1)
    |> Enum.map(&to_priority/1)
    |> Enum.sum()
  end

  defp split_compartments(line) do
    line
    |> Enum.split(div(length(line), 2))
    |> Tuple.to_list()
  end

  defp find_common_items(lists) do
    lists
    |> Enum.map(&MapSet.new/1)
    |> Enum.reduce(&MapSet.intersection/2)
  end

  defp to_priority(item) do
    case item do
      letter when item in ?a..?z -> letter - ?a + 1
      letter when item in ?A..?Z -> letter - ?A + 27
      _ -> raise "Unsupported item #{item}"
    end
  end
end
```
